/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Order.Grade

/-!
### Grading a flag

A flag inherits the grading of its ambient order.
-/

open Set

variable {ğ•† Î± : Type*}

namespace Flag
section PartialOrder
variable [PartialOrder Î±] {s : Flag Î±}

@[simp]
lemma coe_covby_coe {a b : s} : (a : Î±) â‹– b â†” a â‹– b := by
  refine
    and_congr_right'
      âŸ¨fun h c hac => h hac, fun h c hac hcb =>
        @h âŸ¨c, mem_iff_forall_le_or_ge.2 fun d hd => ?_âŸ© hac hcbâŸ©
  classical
  obtain hda | had := le_or_lt (âŸ¨d, hdâŸ© : s) a
  Â· exact Or.inr ((Subtype.coe_le_coe.2 hda).trans hac.le)
  obtain hbd | hdb := le_or_lt b âŸ¨d, hdâŸ©
  Â· exact Or.inl (hcb.le.trans hbd)
  Â· cases h had hdb

@[simp]
lemma isMax_coe {a : s} : IsMax (a : Î±) â†” IsMax a where
  mp h b hab := h hab
  mpr h b hab := by
    refine @h âŸ¨b, mem_iff_forall_le_or_ge.2 fun c hc â†¦ ?_âŸ© hab
    classical
    exact .inr <| hab.trans' <| h.isTop âŸ¨c, hcâŸ©

@[simp]
lemma isMin_coe {a : s} : IsMin (a : Î±) â†” IsMin a where
  mp h b hba := h hba
  mpr h b hba := by
    refine @h âŸ¨b, mem_iff_forall_le_or_ge.2 fun c hc â†¦ ?_âŸ© hba
    classical
    exact .inl <| hba.trans <| h.isBot âŸ¨c, hcâŸ©

variable [Preorder ğ•†]

instance [GradeOrder ğ•† Î±] (s : Flag Î±) : GradeOrder ğ•† s :=
  .liftRight _ (Subtype.strictMono_coe _) fun _ _ => coe_covby_coe.2

instance [GradeMinOrder ğ•† Î±] (s : Flag Î±) : GradeMinOrder ğ•† s :=
  .liftRight _ (Subtype.strictMono_coe _) (fun _ _ => coe_covby_coe.2) fun _ => isMin_coe.2

instance [GradeMaxOrder ğ•† Î±] (s : Flag Î±) : GradeMaxOrder ğ•† s :=
  .liftRight _ (Subtype.strictMono_coe _) (fun _ _ => coe_covby_coe.2) fun _ => isMax_coe.2

instance [GradeBoundedOrder ğ•† Î±] (s : Flag Î±) : GradeBoundedOrder ğ•† s :=
  .liftRight _ (Subtype.strictMono_coe _) (fun _ _ => coe_covby_coe.2) (fun _ => isMin_coe.2)
    fun _ => isMax_coe.2

@[simp, norm_cast]
lemma grade_coe [GradeOrder ğ•† Î±] (a : s) : grade ğ•† (a : Î±) = grade ğ•† a := rfl

end PartialOrder
end Flag
